apply plugin: 'com.github.jk1.tcdeps'
apply plugin: 'java'

version = '1.0'

buildscript {
    // use locally built plugin
    // todo: replace with composite builds?
    dependencies {
        classpath fileTree(dir: '../build/libs', include: '*.jar')
    }
}

repositories {
    teamcityServer {
        url = 'https://teamcity.jetbrains.com'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    // reference arbitrary files as artifacts
    compile tc('bt345:1.0.0-beta-3594:kotlin-compiler-1.0.0-beta-3594.zip')
    // with self-explanatory map dependency notation
    compile tc(buildTypeId: 'bt345', version: '1.0.0-beta-3594', artifactPath: 'kotlin-compiler-for-maven.jar')
    // subfolders are supported
    compile tc('bt345:1.0.0-beta-3594:KotlinJpsPlugin/kotlin-jps-plugin.jar')
    // archive traversal is available with '!' symbol
    compile tc('bt345:1.0.0-beta-3594:kotlin-compiler-1.0.0-beta-3594.zip!/kotlinc/build.txt')
    // Use TeamCity version aliases to declare snapshot-like dependencies
    compile tc('OpenSourceProjects_Peergos_Build:lastFinished:PeergosServer.jar')
    compile tc('bt131:lastPinned:javadocs/index.html')
    compile tc('bt337:lastSuccessful:odata4j.zip')
    compile tc('IntelliJIdeaCe_OpenapiJar:sameChainOrLastFinished:idea_rt.jar')
    // or reference builds by tags with .tcbuildtag suffix
    compile tc('Xodus_Build:hub-1.0.tcbuildtag:console/build/libs/xodus-console.jar')
    // with feature branches supported
    compile tc(buildTypeId: 'bt390', version: 'lastSuccessful', artifactPath: 'updatePlugins.xml', branch: 'master')
    // and basic pattern-matching for artifacts
    compile tc('bt415:lastSuccessful:.*-scala.jar')
}

task listDeps {
    doLast {
        configurations.compile.each {
            dep -> println(dep)
        }
    }
}

