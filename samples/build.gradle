apply plugin: 'com.github.jk1.tcdeps'
apply plugin: 'java'

version = '1.0'

buildscript {
    // use locally built plugin
    // todo: replace with composite builds?
    dependencies {
        classpath fileTree(dir: '../build/libs', include: '*.jar')
    }
}

repositories {
    teamcityServer {
        url = 'https://teamcity.jetbrains.com'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    // reference arbitrary files as artifacts
    implementation tc('OpenSourceProjects_Peergos_Build:2660:peergoslib.nocache.js')
    // with self-explanatory map dependency notation
    implementation tc(buildTypeId: 'bt345', version: '1.1.50-dev-1577', artifactPath: 'kotlin-compiler-for-maven.jar')
    // subfolders are supported
    implementation tc('bt345:1.0.0-beta-3594:KotlinJpsPlugin/kotlin-jps-plugin.jar')
    // archive traversal is available with '!' symbol
    implementation tc('bt345:1.1.50-dev-1577:kotlin-plugin-1.1.50-dev-1577.zip!/Kotlin/kotlinc/build.txt')
    // Use TeamCity version aliases to declare snapshot-like dependencies
    implementation tc('Xodus_Build:lastFinished:crypto/build/libs/xodus-crypto.jar')
    implementation tc('bt131:lastPinned:javadocs/index.html')
    implementation tc('bt337:lastSuccessful:odata4j.zip')
    // or reference builds by tags with .tcbuildtag suffix
    implementation tc('Xodus_Build:tcdeps-jk.tcbuildtag:sshd/build/libs/xodus-sshd.jar')
    // with feature branches supported
    implementation tc(buildTypeId: 'bt390', version: 'lastSuccessful', artifactPath: 'updatePlugins.xml', branch: 'master')
    // and basic pattern-matching for artifacts
    implementation tc('bt415:lastFinished:.*-sources.zip')
}

task listDeps {
    doLast {
        configurations.compileClasspath.each {
            dep -> println(dep)
        }
    }
}

