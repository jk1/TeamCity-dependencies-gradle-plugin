plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
}

allprojects {
    group = 'com.github.jk1'
    description = 'TeamCity dependencies plugin resolves project dependencies via local TeamCity server'
    version = '1.2'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        outputs.upToDateWhen { false }
    }
}

dependencies {
    implementation(project(":tcdeps-kt")) {
        transitive = false
    }
    implementation(project(":tcdeps-groovy")){
        transitive = false
    }
}

jar {
    from { configurations.runtimeClasspath.collect { zipTree(it)  }}
}


// release publishing to gradle plugin repo
pluginBundle {
    website = 'https://github.com/jk1/TeamCity-dependencies-gradle-plugin'
    vcsUrl = 'https://github.com/jk1/TeamCity-dependencies-gradle-plugin'
    description = project.description
    tags = ['dependency-management', 'teamcity']
    plugins {
        teamcityDependenciesPlugin {
            id = 'com.github.jk1.tcdeps'
            displayName = 'Gradle plugin to fetch dependencies from JetBrains TeamCity'
        }
    }
}
