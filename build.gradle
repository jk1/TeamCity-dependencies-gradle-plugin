plugins {
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.20.0'
}

allprojects {

    group = 'com.github.jk1'
    description = 'TeamCity dependencies plugin resolves project dependencies via local TeamCity server'
    version = '1.4'

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        outputs.upToDateWhen { false }
    }
}

configurations {
    subproject
}

dependencies {
    subproject(project(":tcdeps-kt")) {
        transitive = false
    }
    subproject(project(":tcdeps-groovy")){
        transitive = false
    }
}

jar {
    from { configurations.subproject.collect { zipTree(it)  }}
}


// release publishing to gradle plugin repo
pluginBundle {
    website = 'https://github.com/jk1/TeamCity-dependencies-gradle-plugin'
    vcsUrl = 'https://github.com/jk1/TeamCity-dependencies-gradle-plugin'
    description = project.description
    tags = ['dependency-management', 'teamcity']
    plugins {
        teamcityDependenciesPlugin {
            id = 'com.github.jk1.tcdeps'
            displayName = 'Gradle plugin to fetch dependencies from JetBrains TeamCity'
        }
    }
}
